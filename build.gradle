buildscript {
    repositories {
        mavenLocal()
        maven { url "https://nexus.ala.org.au/content/groups/public/" }
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:2.15.1"
    }
}

version "3.0"
group "org.grails.plugins"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin:"org.grails.grails-gsp"
apply plugin:"asset-pipeline"

repositories {
    mavenLocal()
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web-plugin"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:2.15.1"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"

    // webjars
    compile 'org.webjars:underscorejs:1.8.3'
    compile 'org.webjars:jquery:2.2.4'
    compile 'org.webjars.bowergithub.protonet:jquery.inview:1.1.2'
    compile 'org.webjars:handlebars:4.0.11-1'
    compile 'org.webjars.npm:leaflet:0.7.7'
    compile 'org.webjars.npm:leaflet-draw:0.4.14'
    compile('org.webjars.bower:Leaflet.Coordinates:0.1.5') { // TODO get this NPM package into a webjar
        exclude group: 'org.webjars.bower', module: 'leaflet-dist' // exclude leaflet from bower as we have it from npm
    }
    compile 'org.webjars.npm:leaflet-easybutton:1.2.0'
    compile 'org.webjars.npm:leaflet-control-geocoder:1.5.5'
    compile 'org.webjars.npm:leaflet.markercluster:0.5.0'
    compile 'org.webjars.npm:leaflet-loading:0.1.24'
    compile 'org.webjars.npm:leaflet-sleep:0.5.1'
    compile 'org.webjars:font-awesome:4.7.0'
    compile 'org.webjars.npm:urijs:1.18.10'

//    compile 'org.grails.plugins:grails-google-visualization:2.2.1'
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
}
// enable if you wish to package this plugin as a standalone application
bootRepackage.enabled = false
grailsPublish {
    // TODO: Provide values here
//    user = 'user'
//    key = 'key'
    websiteUrl = 'https://github.com/AtlasOfLivingAustralia/ala-map-plugin'
    issueTrackerUrl = 'https://github.com/AtlasOfLivingAustralia/ala-map-plugin/issues'
    vcsUrl = 'https://github.com/AtlasOfLivingAustralia/ala-map-plugin'
    githubSlug = 'AtlasOfLivingAustralia/ala-map-plugin'
    license {
        name = 'MPL-2.0'
    }
    title = "ALA Map Plugin"
    desc = "Provides occurence maps for ALA applications"
    developers = ["ALA":"support@ala.org.au"]
}


assets {
    packagePlugin = true
    minifyJs = true
}

publishing {
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases' }"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }
    publications {
        mavenJar(MavenPublication) {
            pom.withXml {
                def pomNode = asNode()
                pomNode.dependencyManagement.replaceNode {}

                // simply remove dependencies without a version
                // version-less dependencies are handled with dependencyManagement
                // see https://github.com/spring-gradle-plugins/dependency-management-plugin/issues/8 for more complete solutions
                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }
            from components.java

        }
    }
}